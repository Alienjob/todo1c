//Добавляет задачу в табличную часть задач
// Параметры:
//    ТекстЗадачи - строка(100) - Короткое описание задачи
Процедура ДобавитьЗадачу(ТекстЗадачи) Экспорт
	
	НоваяЗадача = СписокЗадач.Добавить();
	НоваяЗадача.Выполнена = Ложь;
	НоваяЗадача.Текст = ТекстЗадачи;
	
КонецПроцедуры
 
//Выполняет задачу по индексу
// Параметры:
//    ИндексЗадачи - число - индекс задачи в табличной части
Процедура ВыполнитьЗадачу(ИндексЗадачи) Экспорт
	
	СписокЗадач[ИндексЗадачи].Выполнена = Истина;
	
КонецПроцедуры

//Сохраняет текущее состояние задач в базу
Процедура СохранитьЗадачи() Экспорт
	
	Если Метаданные.РегистрыСведений.Найти("Задачи") = неопределено Тогда
		Сообщить("Сохранение невозможно - отсутствует необходимый регистр");
		Возврат;
	КонецЕсли;
	
	НЗ = РегистрыСведений.Задачи.СоздатьНаборЗаписей();
	Для каждого стрЗадачи Из СписокЗадач Цикл
		Запись = НЗ.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, стрЗадачи);
		Запись.id = Новый УникальныйИдентификатор;
	КонецЦикла; 
	
	НЗ.Записать();
	
КонецПроцедуры

//Читает задачи из базы
Процедура ПрочитатьЗадачи() Экспорт
	
	Если Метаданные.РегистрыСведений.Найти("Задачи") = неопределено Тогда
		Сообщить("Загрузка сохраненных задач невозможна - отсутствует необходимый регистр");
		Возврат;
	КонецЕсли;
	
    Запрос = Новый Запрос;
    Запрос.Текст =
    "ВЫБРАТЬ
    |	Задачи.Выполнена,
    |	Задачи.Текст
    |ИЗ
    |	РегистрСведений.Задачи КАК Задачи";
    
    Результат = Запрос.Выполнить();
    СписокЗадач.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры
 